// Top-level build file where you can add configuration options common to all sub-projects/modules.
apply from: "config.gradle"
buildscript {
    ext.kotlin_version = "1.4.20"
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:4.0.2"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.google.dagger:hilt-android-gradle-plugin:2.32-alpha'
        classpath 'com.didichuxing.doraemonkit:dokitx-plugin:3.3.5'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        google()
        jcenter()
        maven { url 'https://jitpack.io' }
    }
}

//子工程模块通用配置信息
subprojects { subproject ->
    if (subproject.getName().startsWith("biz_") && subproject.getName() != "biz_provider") {

        apply plugin: 'com.android.library'
        apply plugin: 'kotlin-android'
        apply plugin: 'kotlin-android-extensions'
        apply plugin: 'kotlin-kapt'
        apply plugin: 'dagger.hilt.android.plugin'

        kapt {
            arguments {
                arg("AROUTER_MODULE_NAME", subproject.getName())
            }
        }

        android {
            compileSdkVersion rootProject.ext.android.compileSdkVersion
            buildToolsVersion rootProject.ext.android.buildToolsVersion

            defaultConfig {
                minSdkVersion rootProject.ext.android.minSdkVersion
                targetSdkVersion rootProject.ext.android.targetSdkVersion
                versionCode rootProject.ext.android.versionCode
                versionName rootProject.ext.android.versionName

                consumerProguardFiles 'consumer-rules.pro'
                testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
            }

            buildTypes {
                release {
                    minifyEnabled false
                    proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
                }
            }

            buildFeatures {
                dataBinding = true
            }

            compileOptions {
                sourceCompatibility JavaVersion.VERSION_1_8
                targetCompatibility JavaVersion.VERSION_1_8
            }

            kotlinOptions {
                jvmTarget = "1.8"
            }
        }

        dependencies {
            implementation fileTree(dir: "libs", include: ["*.jar"])
            implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
            api project(path: ':biz_provider')
            testImplementation rootProject.ext.androidx["junit"]
            androidTestImplementation rootProject.ext.androidx["androidx.test.ext-junit"]
            androidTestImplementation rootProject.ext.androidx["androidx.test.espresso:espresso-core"]

            api rootProject.ext.dependencies["hilt-android"]
            kapt rootProject.ext.dependencies["arouter-compiler"]
            kapt rootProject.ext.dependencies["hilt-android-compiler"]
            kapt rootProject.ext.androidx["hilt-compiler"]
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}